
function(check_magick BLDTARGET)
    if(NOT DEFINED ImageMagick_LIBRARIES)
        find_package(ImageMagick REQUIRED COMPONENTS Magick++ MagickCore)
    endif()

    string(REGEX MATCH "[Qq](16|8)(HDRI)?" MAGICK_QUANTUM ${ImageMagick_LIBRARIES})
    if(${CMAKE_MATCH_COUNT} GREATER_EQUAL "1")
        if (NOT DEFINED MAGICK_QUANTUM_DEPTH)
            if(${CMAKE_MATCH_1} EQUAL "16")
                message(STATUS "Setting Quantum depth to 16.")
                set(MAGICK_QUANTUM_DEPTH "16")
            elseif(${CMAKE_MATCH_1} EQUAL "8")
                message(STATUS "Setting Quantum depth to 8.")
                set(MAGICK_QUANTUM_DEPTH "8")
            else()
                message(SEND_ERROR "Couldn't determine quantum depth, please define MAGICK_QUANTUM_DEPTH and MAGICK_HDRI_ENABLE manually.")
            endif()
        endif()
        if (NOT DEFINED MAGICK_HDRI_ENABLE)
            if(${CMAKE_MATCH_COUNT} EQUAL "2")
                message(STATUS "Enabling HDRI")
                set(MAGICK_HDRI_ENABLE "1")
            else()
                message(STATUS "Disabling HDRI")
                set(MAGICK_HDRI_ENABLE "0")
            endif()
        endif()
    endif()
    
    include_directories(${ImageMagick_INCLUDE_DIRS})
    target_link_libraries(${BLDTARGET} ${ImageMagick_LIBRARIES})
    target_compile_definitions(${BLDTARGET} PUBLIC
        MAGICKCORE_QUANTUM_DEPTH=${MAGICK_QUANTUM_DEPTH}
        MAGICKCORE_HDRI_ENABLE=${MAGICK_HDRI_ENABLE}
    )
endfunction()
